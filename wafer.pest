WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

module = { SOI ~ (function)* ~ EOI }

operation = { "+" | "-" | "*" | "/" }
number = @{ ASCII_DIGIT+ }
identifier = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }

args = { "(" ~ (expression ~ ("," ~ expression)*)? ~ ")" }

assignment_expression = { identifier ~ ":=" ~ expression }
arithmetic_expression = { primary_expression ~ (operation ~ primary_expression)* }
call_expression = { identifier ~ args }
primary_expression = _{ ("(" ~ expression ~ ")") | number | call_expression | identifier }
expression = _{ assignment_expression | arithmetic_expression }

let_statement = { "let" ~ identifier ~ "=" ~ expression ~ ";" }
expression_statement = { expression ~ ";" }
statement = _{ let_statement | expression_statement }

params = { "(" ~ (identifier ~ ("," ~ identifier)*)? ~ ")" }
block_expression = { "{" ~ (statement)* ~ expression ~ "}" }
function = { "func" ~ identifier ~ params ~ block_expression } 
